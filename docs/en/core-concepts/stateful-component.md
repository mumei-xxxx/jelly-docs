# Stateful Component

## Signal and Atom

`Signal` is a type to represent a value that changes over time.

And if `Signal` is the output, then `Atom` represents the input.
You can manipulate values with the `send` and `patch` functions.

`Signal` and `Atom` can be created using `signal` function.

### Example

Let's make a simple counter.

```preview
counter
```

```purescript
module Example.Counter where

import Prelude

import Data.Tuple.Nested ((/\))
import Jelly.Data.Component (Component, el, el', text, textSig)
import Jelly.Data.Hooks (hooks)
import Jelly.Data.Prop (on)
import Jelly.Data.Signal (patch_, signal)
import Web.HTML.Event.EventTypes (click)

counterExample :: forall context. Component context
counterExample = hooks do
  countSig /\ countAtom <- signal 0

  pure do
    el "button" [ on click \_ -> patch_ countAtom (_ + 1) ] $ text "Increment"
    el "button" [ on click \_ -> patch_ countAtom (_ - 1) ] $ text "Decrement"
    el' "div" do
      text "Count: "
      textSig $ show <$> countSig

```

## `signalEq` function

Signal and Atom pairs generated by the `signal` function are updated when the same value as the current value is submitted.
With the `signalEq` function, the pair will not be updated if the same value as the current value is submitted.

The types are as follows, respectively.

```purescript
signal :: forall m a. MonadEffect m => a -> m (Signal a /\ Atom a)
signalEq :: forall m a. MonadEffect m => Eq a => a -> m (Signal a /\ Atom a)
```

### Example

```preview
signalEq
```

```purescript
module Example.SignalEq where

import Prelude

import Data.Tuple.Nested ((/\))
import Jelly.Data.Component (Component, el, el', text, textSig)
import Jelly.Data.Hooks (hooks)
import Jelly.Data.Prop (on)
import Jelly.Data.Signal (patch_, send, signal, signalEq)
import Jelly.Hooks.UseSignal (useSignal)
import Web.HTML.Event.EventTypes (click)

signalEqExample :: forall context. Component context
signalEqExample = hooks do
  signalWithoutEq /\ atomWithoutEq <- signal true
  signalWithEq /\ atomWithEq <- signalEq true

  updateCountWithoutEqSig /\ updateCountWithoutEqAtom <- signalEq 0
  updateCountWithEqSig /\ updateCountWithEqAtom <- signalEq 0

  useSignal do
    _ <- signalWithoutEq
    pure do
      patch_ updateCountWithoutEqAtom $ add 1
      mempty

  useSignal do
    _ <- signalWithEq
    pure do
      patch_ updateCountWithEqAtom $ add 1
      mempty

  pure do
    el "button" [ on click \_ -> send atomWithoutEq true *> send atomWithEq true ] $ text "True"
    el "button" [ on click \_ -> send atomWithoutEq false *> send atomWithEq false ] $ text "False"
    el' "div" do
      text "Update count without Eq: "
      textSig $ show <$> updateCountWithoutEqSig
    el' "div" do
      text "Update count with Eq: "
      textSig $ show <$> updateCountWithEqSig
```
