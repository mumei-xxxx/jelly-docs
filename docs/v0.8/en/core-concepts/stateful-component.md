# Stateful Component

## Signal and Channel

`Signal` is a type to represent a value that changes over time.

And if `Signal` is the output, then `Channel` represents the input.
You can manipulate values with the `writeChannel` and `modifyChannel` functions.

`Signal` and `Channel` can be created using `newState` function.

You can display `Signal String` by `textSig` function.

### Example

Let's make a simple counter.

```preview
counter
```

```purescript
module Example.Counter where

import Prelude

import Data.Tuple.Nested ((/\))
import Jelly.Component (class Component, text, textSig)
import Jelly.Element as JE
import Jelly.Prop (on)
import Signal (modifyChannel, newState)
import Web.HTML.Event.EventTypes (click)

counterExample :: forall m. Component m => m Unit
counterExample = do
  countSig /\ countChannel <- newState 0

  JE.button [ on click \_ -> modifyChannel countChannel (_ + 1) ] $ text "Increment"
  JE.button [ on click \_ -> modifyChannel countChannel (_ - 1) ] $ text "Decrement"
  JE.div' do
    text "Count: "
    textSig $ show <$> countSig

```

## `newStateEq` function

Signal and Channel pairs generated by the `signal` function are updated when the same value as the current value is submitted.
With the `newStateEq` function, the pair will not be updated if the same value as the current value is submitted.

The types are as follows, respectively.

```purescript
signal :: forall m a. MonadEffect m => a -> m (Signal a /\ Channel a)
newStateEq :: forall m a. MonadEffect m => Eq a => a -> m (Signal a /\ Channel a)
```

### Example

```preview
newStateEq
```

```purescript
module Example.SignalEq where

import Prelude

import Data.Tuple.Nested ((/\))
import Jelly (Component, hooks, on, text, textSig)
import Jelly.Data.Signal (modifyChannel_, newState, newStateEq, writeChannel)
import Jelly.Element as JE
import Jelly.Hooks (useEffect_)
import Web.HTML.Event.EventTypes (click)

signalEqExample :: forall context. Component context
signalEqExample = hooks do
  signalWithoutEq /\ atomWithoutEq <- newState true
  signalWithEq /\ atomWithEq <- newStateEq true

  updateCountWithoutEqSig /\ updateCountWithoutEqChannel <- newStateEq 0
  updateCountWithEqSig /\ updateCountWithEqChannel <- newStateEq 0

  useEffect_ do
    _ <- signalWithoutEq
    pure do
      modifyChannel_ updateCountWithoutEqChannel $ add 1

  useEffect_ do
    _ <- signalWithEq
    pure do
      modifyChannel_ updateCountWithEqChannel $ add 1

  pure do
    JE.button [ on click \_ -> writeChannel atomWithoutEq true *> writeChannel atomWithEq true ] $ text "True"
    JE.button [ on click \_ -> writeChannel atomWithoutEq false *> writeChannel atomWithEq false ] $ text "False"
    JE.div' do
      text "Update count without Eq: "
      textSig $ show <$> updateCountWithoutEqSig
    JE.div' do
      text "Update count with Eq: "
      textSig $ show <$> updateCountWithEqSig

```
