# Stateful Component

## Signal and Atom

`Signal` is a type to represent a value that changes over time.

And if `Signal` is the output, then `Atom` represents the input.
You can manipulate values with the `writeAtom` and `modifyAtom` functions.

`Signal` and `Atom` can be created using `newState` function.

You can display `Signal String` by `textSig` function.

### Example

Let's make a simple counter.

```preview
counter
```

```purescript
module Example.Counter where

import Prelude

import Data.Tuple.Nested ((/\))
import Jelly (Component, hooks, on, text, textSig)
import Jelly.Data.Signal (modifyAtom_, newStateEq)
import Jelly.Element as JE
import Web.HTML.Event.EventTypes (click)

counterExample :: forall context. Component context
counterExample = hooks do
  countSig /\ countAtom <- newStateEq 0

  pure do
    JE.button [ on click \_ -> modifyAtom_ countAtom (_ + 1) ] $ text "Increment"
    JE.button [ on click \_ -> modifyAtom_ countAtom (_ - 1) ] $ text "Decrement"
    JE.div' do
      text "Count: "
      textSig $ show <$> countSig

```

## `newStateEq` function

Signal and Atom pairs generated by the `signal` function are updated when the same value as the current value is submitted.
With the `newStateEq` function, the pair will not be updated if the same value as the current value is submitted.

The types are as follows, respectively.

```purescript
signal :: forall m a. MonadEffect m => a -> m (Signal a /\ Atom a)
newStateEq :: forall m a. MonadEffect m => Eq a => a -> m (Signal a /\ Atom a)
```

### Example

```preview
newStateEq
```

```purescript
module Example.SignalEq where

import Prelude

import Data.Tuple.Nested ((/\))
import Jelly (Component, hooks, on, text, textSig)
import Jelly.Data.Signal (modifyAtom_, newState, newStateEq, writeAtom)
import Jelly.Element as JE
import Jelly.Hooks (useEffect_)
import Web.HTML.Event.EventTypes (click)

signalEqExample :: forall context. Component context
signalEqExample = hooks do
  signalWithoutEq /\ atomWithoutEq <- newState true
  signalWithEq /\ atomWithEq <- newStateEq true

  updateCountWithoutEqSig /\ updateCountWithoutEqAtom <- newStateEq 0
  updateCountWithEqSig /\ updateCountWithEqAtom <- newStateEq 0

  useEffect_ do
    _ <- signalWithoutEq
    pure do
      modifyAtom_ updateCountWithoutEqAtom $ add 1

  useEffect_ do
    _ <- signalWithEq
    pure do
      modifyAtom_ updateCountWithEqAtom $ add 1

  pure do
    JE.button [ on click \_ -> writeAtom atomWithoutEq true *> writeAtom atomWithEq true ] $ text "True"
    JE.button [ on click \_ -> writeAtom atomWithoutEq false *> writeAtom atomWithEq false ] $ text "False"
    JE.div' do
      text "Update count without Eq: "
      textSig $ show <$> updateCountWithoutEqSig
    JE.div' do
      text "Update count with Eq: "
      textSig $ show <$> updateCountWithEqSig

```
